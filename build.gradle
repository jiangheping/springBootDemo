buildscript {

	//全局变量定义
	ext {
		springBootVersion = '2.0.3.RELEASE'
	}

	repositories {
		//maven默认的中央仓库
		mavenCentral()
	}
	dependencies {
		//spring boot插件 v2.0.3
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'com'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}


dependencies {
	testCompile('org.springframework.boot:spring-boot-starter-test')//springBoot test
	compile('org.springframework.boot:spring-boot-starter')//springBoot starter
	compile('org.springframework.boot:spring-boot-starter-web')//web容器
	compile('org.projectlombok:lombok:1.16.20')//lombok

	//整合mybatis
	compile group: 'org.mybatis.spring.boot', name: 'mybatis-spring-boot-starter', version: '1.3.1' //mybatis
	compile group: 'com.alibaba', name: 'druid-spring-boot-starter', version: '1.1.9' //druid连接池
	compile group: 'mysql', name: 'mysql-connector-java', version: '5.1.36'

}

//mybatis自动生成 start
configurations {
	mybatisGenerator
}

dependencies {
	mybatisGenerator 'org.mybatis.generator:mybatis-generator-core:1.3.2'
	mybatisGenerator 'mysql:mysql-connector-java:5.1.36'
	mybatisGenerator 'tk.mybatis:mapper:3.3.2'
}

def getDbProperties = {
	def properties = new Properties()
	file("src/main/resources/application.properties").withInputStream { inputStream ->
		properties.load(inputStream)
	}
	properties
}

task mybatisGenerate << {
	def properties = getDbProperties()
	ant.properties['targetProject'] = projectDir.path
	ant.properties['driverClass'] = properties.getProperty("spring.datasource.druid.driver-class-name")
	ant.properties['connectionURL'] = properties.getProperty("spring.datasource.druid.url")
	ant.properties['userId'] = properties.getProperty("spring.datasource.druid.username")
	ant.properties['password'] = properties.getProperty("spring.datasource.druid.password")
	ant.properties['src_main_java'] = sourceSets.main.java.srcDirs[0].path
	ant.properties['src_main_resources'] = sourceSets.main.resources.srcDirs[0].path
	ant.properties['modelPackage'] = properties.getProperty("mybatis.generator.model")
	ant.properties['mapperPackage'] = properties.getProperty("mybatis.generator.mapper")
	ant.properties['sqlMapperPackage'] = properties.getProperty("mybatis.generator.xml")
	ant.taskdef(
			name: 'mbgenerator',
			classname: 'org.mybatis.generator.ant.GeneratorAntTask',
			classpath: configurations.mybatisGenerator.asPath
	)
	ant.mbgenerator(overwrite: true,
			configfile: 'src/main/resources/generatorConfig.xml', verbose: true) {
		propertyset {
			propertyref(name: 'targetProject')
			propertyref(name: 'userId')
			propertyref(name: 'driverClass')
			propertyref(name: 'connectionURL')
			propertyref(name: 'password')
			propertyref(name: 'src_main_java')
			propertyref(name: 'src_main_resources')
			propertyref(name: 'modelPackage')
			propertyref(name: 'mapperPackage')
			propertyref(name: 'sqlMapperPackage')
		}
	}
}
//mybatis自动生成 end


